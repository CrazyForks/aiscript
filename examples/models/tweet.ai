use lib.utils

@table(name="tweet", schema="public")
model Tweet {
    @primary
    id: int
    user_id: int
    content: str
    created_at: datetime
    updated_at: datetime
}

with Tweet {
    @one_to_one(model.User, user_id)
    user: model.User

    auto fn get_by_id(id: int) -> Tweet

    auto fn get_by_user_id(user_id: int) -> [Tweet]

    auto fn delete_by_id(id: int) -> bool

    fn create(user_id: int, content: str) -> Tweet {
        return sql {
            INSERT INTO tweet (user_id, content)
            VALUES (:user_id, :content) RETURNING *
        }
    }

    fn update(id: int, content: str) -> Tweet {
        return sql {
            UPDATE tweet SET content = :content WHERE id = :id RETURNING *
        }
    }
}